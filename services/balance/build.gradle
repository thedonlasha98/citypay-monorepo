plugins {
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.2'
    id 'java'
    id 'com.diffplug.spotless' version '6.25.0'
}

bootJar {
    archiveBaseName = 'balance'
    archiveVersion = '0.0.1'
}

group = 'io.citypay.core.balance'
version = '0.0.1'
description = 'Balance Microservice'

// Google Java Format configuration
spotless {
    java {
        googleJavaFormat('1.22.0').style('GOOGLE')
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()

        // Import order for your project
        importOrder('java', 'javax', 'org', 'com', 'io.citypay', '')
    }
}

dependencies {
    implementation project(':libs:shared')
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.12'
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}

// Auto-format before compile
compileJava.dependsOn spotlessApply